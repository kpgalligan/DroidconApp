buildscript {
    ext.kotlin_version = '1.0.3'
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'kotlin-android-extensions'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def conventionid = properties.getProperty('conventionid')
def baseurl = properties.getProperty('baseurl')
def voteends = properties.getProperty('voteends')
def releasePassword = properties.getProperty('releasePassword')
def crashlyticsApiKey = properties.getProperty('crashlyticsApiKey')
def jwLicenseKey = properties.getProperty('jwLicenseKey')

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

android {
    compileSdkVersion 23
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "co.touchlab.droidconandroid"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 917
        versionName "0.9.17"

//        multiDexEnabled true

//        buildConfigField "int", "CONVENTION_ID", "$conventionid"
        buildConfigField "String", "BASE_URL", "\"$baseurl\""
//        buildConfigField "String", "VOTE_ENDS", "\"$voteends\""
        resValue "string", "crashlytics_api_key", "$crashlyticsApiKey"
        resValue "string", "jw_license_key", "$jwLicenseKey"
    }

    productFlavors {
        albany {
            applicationId "co.touchlab.droidcon.albany"
        }
        sf {
            applicationId "co.touchlab.droidcon.sf"
        }
        nyc {
            applicationId "co.touchlab.droidconandroid"
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        staging {
            storeFile file("release.jks")
            keyAlias "android"
            storePassword "$releasePassword"
            keyPassword "$releasePassword"
        }
        release {
            storeFile file("release.jks")
            keyAlias "android"
            storePassword "$releasePassword"
            keyPassword "$releasePassword"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        staging {
            debuggable true
            minifyEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/org.apache.commons/commons-lang3/pom.xml'
        exclude 'META-INF/maven/org.apache.commons/commons-lang3/pom.properties'
        exclude 'jsr305_annotations/Jsr305_annotations.gwt.xml'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }
}

configurations {
//    compile.exclude module: 'magicthreads'
    compile.exclude module: 'intellijannotations'
    compile.exclude module: 'android'
    compile.exclude module: 'androidbase'
}

dependencies {
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.squareup.picasso:picasso:2.4.0'
//    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
//    compile 'de.greenrobot:eventbus:2.2.1'
    compile project(':profilephotoeditor')
    //    compile project (":zxing")
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.github.johnpersano:supertoasts:1.3.4.1@aar'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'co.touchlab.squeaky:squeaky-query:0.5.0.0'
    compile 'com.google.android.gms:play-services-identity:9.0.0'
    compile 'com.google.android.gms:play-services-plus:9.0.0'
    /*compile('com.github.androidsocialnetworks:library:0.3.2@aar') {
              transitive = true
          }*/
//    provided 'com.intellij:annotations:9.0.4'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.wnafee:vector-compat:1.0.5'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.android.support:palette-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:customtabs:23.1.1'
    compile 'com.google.firebase:firebase-core:9.0.0'
    compile 'com.google.firebase:firebase-crash:9.0.0'
//    compile 'com.google.firebase:firebase-invites:9.0.0'
    compile 'com.google.firebase:firebase-messaging:9.0.0'

//    compile 'com.android.support:multidex:1.0.1'
//    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
//        transitive = true;
//    }
//
    compile project(':lib')
    compile project(':jwplayer-android-sdk-2.4.1+158')
}
apply plugin: 'com.google.gms.google-services'
